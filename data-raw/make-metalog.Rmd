---
title: "Producing metalog functions in Stan"
author: "Deemah"
date: "2/16/2021"
output: html_document
---

```{r setup, include=FALSE}
knitr::opts_chunk$set(echo = TRUE)
```

# Objective

Fit metalog beyond simple symmetrical triplet.
Ultimately, we want to sample from Dirichlet distribution and transform the triplet of values into the three quantiles. Then we want to fit metalog to these values.


```{r createfile}
stan_code <- tempfile("metalog_funs", fileext = ".stan")
```

```{cat, echo=FALSE, engine.opts=list(file=stan_code, append = TRUE), class.source="stan"}
functions{ // begin the functions block
```

## Converting QPP pairs to A-coefficients

First we are going to make a service function which will fit metalog to QPP (quantile-probability parameter) pairs. The function accepts two vectors, assumes that the number of metalog terms is determined by the number of QPP pairs, and returns a vector of a-coefficients describing the metalog.

Todo: feasibility check

```{cat metalogCoeff, engine.opts=list(file=stan_code, append = TRUE), class.source="stan"}
vector fit_metalog(vector p, vector quantiles) {
    // code by Ben Goodrich <benjamin.goodrich@columbia.edu> 
    int n = rows(p);
    matrix[n, n] Y;
    vector[n] log_odds = n > 1 ? logit(p) : rep_vector(not_a_number(), n);
    vector[n] pmhalf =   n > 2 ? p - 0.5  : rep_vector(not_a_number(), n);
    int odd = 1;
    if (n == 0) reject("p cannot be of size zero");
    if (rows(quantiles) != n) reject("p and quantiles must be of the same size");
  
    Y[ , 1] = rep_vector(1, n);
    if (n > 1) Y[ , 2] = log_odds;
    if (n > 2) Y[ , 3] = pmhalf .* log_odds;
    if (n > 3) Y[ , 4] = pmhalf;
    for (m in 5:n) {
      if (odd) {
          pmhalf .*= pmhalf;
          Y[ , m]  = pmhalf;
        } else Y[ , m] = pmhalf .* log_odds;
        odd = odd == 0;
    }
    return Y \ quantiles;
  }
```

## Metalog quantile function

```{cat metalogQF, engine.opts=list(file=stan_code, append = TRUE), class.source="stan"}
real metalog_qf_s_cdf(real y, vector a){
    // code by Dmytro Perepolkin <dperepolkin@gmail.com>
    int n = rows(a);
    real res=a[1];
    real logity = logit(y);
    real ymhalf = y-0.5;
    int odd = 1;
    if (n == 0) reject("a cannot be of size zero");
    if (n > 1) res += a[2]*logity;
    if (n > 2) res += a[3]*ymhalf*logity;
    if (n > 3) res += a[4]*ymhalf;
    for (m in 5:n) {
      if (odd) {
          res += a[m]*pow(ymhalf, (m-1)/2.0);
        } else res += a[m]*pow(ymhalf, m/2.0-1)*logity;
        odd = odd == 0;
    }
    return res;
}

real metalog_lqf_s_lcdf(real y, vector a){
  return log(metalog_qf_s_cdf(y, a));
}

real metalog_rng(vector a) {
    return metalog_qf_s_cdf(uniform_rng(0, 1), a);
}
```

## Metalog quantile density function

Quantile density function

```{cat metalogQDF, engine.opts=list(file=stan_code, append = TRUE), class.source="stan"}
real metalog_qdf_s_pdf(real y, vector a){
    // code by Dmytro Perepolkin <dperepolkin@gmail.com>
    int n = rows(a);
    real res;
    real yt1my = y*(1-y);
    real logity = logit(y);
    real ymhalf = y-0.5;
    int odd = 1;
    if (n == 0) reject("a cannot be of size zero");
    if (n > 1) res = a[2]/yt1my;
    if (n > 2) res += a[3]*(ymhalf/yt1my+logity);
    if (n > 3) res += a[4];
    for (m in 5:n) {
      if (odd) {
          res += a[m]*(m-1)/2.0*pow(ymhalf, (m-3)/2.0);
        } else res += a[m]*(pow(ymhalf, m/2.0-1)/yt1my+(m/2.0-1)*pow(ymhalf, m/2.0-2)*logity);
        odd = odd == 0;
    }
    return res;
 }

 real metalog_lqdf_s_lpdf(real y, vector a){
    // code by Dmytro Perepolkin <dperepolkin@gmail.com>
    return log(metalog_qdf_s_pdf(y, a));
 }
```

## Metalog density of quantile function

This is the density of the quantile function, or reciprocal quantile density function (RQDF).

```{cat metalogRQDF, engine.opts=list(file=stan_code, append = TRUE), class.source="stan"}

 real metalog_rqdf_s_pdf(real y, vector a){
    // code by Dmytro Perepolkin <dperepolkin@gmail.com>
    return inv(metalog_qdf_s_pdf(y, a));
 }

 real metalog_lrqdf_s_lpdf(real y, vector a){
    // code by Dmytro Perepolkin <dperepolkin@gmail.com>
    return log(inv(metalog_qdf_s_pdf(y, a)));
 }

```

# Metalog root function

This is method described in Gilchrist p 99 and p 210
Nair Quantile-Based Reliability Analysis p 345

```{cat metalogRoot, engine.opts=list(file=stan_code, append = TRUE), class.source="stan"}

 vector metalog_approx_u(vector x, vector a, real tol){
  // goal-seeking function for passing the parameters as data
   int N=rows(x);
   vector[N] u_guess = cumulative_sum(rep_vector(1, N))/(N+1); // 1:N/(N+1)
   vector[N] u;
   real u0;
   for(i in 1:N){
     u0 = u_guess[i];
     while(fabs(metalog_qf_s_cdf(u0, a)- x[i]) > tol){
       u0 += (x[i]-metalog_qf_s_cdf(u0, a))/metalog_qdf_s_pdf(u0, a);
       u0 = fmin(fmax(u0, tol), 1-tol);
     }
     u[i] = u0;
   }
   return u;
  }
```


```{cat, echo=FALSE, engine.opts=list(file=stan_code, append = TRUE), class.source="stan"}
} // finish the functions block
```

```{r}
file.copy(stan_code, "metalog_funs.stan")
#writeLines(readLines(stan_code))
#readr::write_lines(readLines(stan_code), "metalog_funs.stan")
#expose_stan_functions("metalog.stan")
file.remove(stan_code)
```


